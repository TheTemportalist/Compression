// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

// START
apply plugin: 'scala'
// END

ext.getBuildNumber = {
    if (System.getenv("BUILD_NUMBER") != null) return "${System.getenv("BUILD_NUMBER")}"
    else if (project.hasProperty("bambooBuildNumber")) return project.bambooBuildNumber
    else return getDate()
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('MM-dd-yyyy_HH-mm-ss')
    return formattedDate
}

group = "${org}.${groupName}"
archivesBaseName = "${archiveName}"

minecraft {
    version = "${version_minecraft}-${version_forge}"
    runDir = "run"
    mappings = "${version_mcp}"

    replace "required-after:Forge", "required-after:Forge@[${version_forge},)"
    replace "required-after:Origin", "required-after:Origin@[${version_origin},)"
    replace "@MOD_VERSION", "${version_major}.${version_minor}.${version_patch}"
}

version = "${project.minecraft.version}-${version_major}.${version_minor}.${version_patch}_" + getBuildNumber()

repositories {
    mavenCentral()
    jcenter()
    maven {
        name "dsiriusArt"
        url "http://box1.dmodoomsirius.me:8081/artifactory/libs-release/"
    }
}

dependencies {
    deobfCompile "temportalist.origin:Origin:9.0.+"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

}
